
pipeline {
    agent {
        node {
            label 'linux'
        }
    }

    options {
        buildDiscarder(logRotator(numToKeepStr: '10'))
    }

    parameters {
        booleanParam(name: 'MAVEN_DEBUG', defaultValue: false, description: 'Enable maven debug logs')
    }

    environment {
        SONAR_HOST_URL = 'https://sonarcloud.io'
        MAVEN_ARGS = "--batch-mode --errors --fail-at-end --show-version --no-transfer-progress -DinstallAtEnd=true -DdeployAtEnd=true -s settings.xml"
        JAVA_HOME = tool name: 'temurin-17_linux', type: 'jdk'
        PATH = "${JAVA_HOME}/bin:${PATH}"
    }

    stages {
        stage('Validation') {
            steps {
                script {
                    def manual = false
                    def causes = currentBuild.getBuildCauses()
                    for(cause in causes) {
                        if (cause.shortDescription.toString().contains("Started by user")) {
                            manual = true
                        }
                    }
                    def skipCi = sh (script: "git log -1 --pretty=%B | grep '\\[skip ci\\]'", returnStatus: true)
                    echo "${skipCi}"
                    echo "${env.TAG_NAME}"
                    if (manual == false && skipCi == 0 && env.TAG_NAME == null) {
                        echo "Skipping this pipeline by commit message mark!"
                        currentBuild.result = "ABORTED"
                        throw new org.jenkinsci.plugins.workflow.steps.FlowInterruptedException(hudson.model.Result.ABORTED)
                    } else {
                        echo "pipeline build..."
                    }
                }
            }
        }
        stage('Build') {
            steps {
                withCredentials([
                        file(credentialsId: 'GPG_PRI', variable: 'GPG_PRI'),
                        string(credentialsId: 'GPG_PASSPHRASE', variable: 'MAVEN_GPG_PASSPHRASE'),
                        usernamePassword(credentialsId: 'opcal-ossrh', passwordVariable: 'OSSRH_P', usernameVariable: 'OSSRH_U'),
                        string(credentialsId: 'httpbin-ci-token', variable: 'CI_TOKEN')
                ]) {
                    script {
                        sh "gpg --allow-secret-key-import --batch --import \$GPG_PRI"
                        if (params.MAVEN_DEBUG == true) {
                            env.MAVEN_CLI_DEBUG = " -e -X";
                        } else {
                            env.MAVEN_CLI_DEBUG = "";
                        }
                        if (env.CHANGE_URL == null) {
                            // non-PR build
                            sh "./mvnw -U clean ${env.MAVEN_CLI_DEBUG} deploy -Pbuild"
                        } else {
                            // PR build
                            sh "./mvnw -U clean ${env.MAVEN_CLI_DEBUG} test -Pbuild -Dgpg.skip=true"
                        }
                    }
                }
            }
        }
        stage('Analysis') {
            steps {
                withCredentials([
                        string(credentialsId: 'SONAR_TOKEN-opcal-cloud-commons', variable: 'SONAR_TOKEN'),
                        usernamePassword(credentialsId: 'ossindex-token', passwordVariable: 'OSSINDEX_P', usernameVariable: 'OSSINDEX_U'),
                ]) {
                    script {
                        sh "./mvnw sonar:sonar -Psonar-config -Dsonar.branch.name=${env.GIT_BRANCH}"
                        sh "./mvnw ossindex:audit -Pbuild"
                    }
                }
                withSonarQubeEnv('sqscan') {
                    sh """
                        ./mvnw sonar:sonar -Psonar-config -Dsonar.projectKey=xyz.opcal.cloud:opcal-cloud-commons -Dsonar.projectName='opcal-cloud-commons'
                    """
                }
            }
        }
    }
    post {
        always {
            cleanWs()
        }
        failure {
            script {
                env.AI_EXPLAIN = explainError(
                        maxLines: 10000,
                        logPattern: '(?i)(error|failed|exception)'
                )
            }
            withCredentials([
                    string(credentialsId: 'ALERT_MAIL', variable: 'SEND_TO')
            ]) {
                emailext(
                        to: "${SEND_TO}",
                        subject: "${env.JOB_NAME} - Build # ${env.BUILD_NUMBER} - ${currentBuild.currentResult}!",
                        body: "${env.JOB_NAME} - Build # ${env.BUILD_NUMBER} - ${currentBuild.currentResult}. \n\n${env.AI_EXPLAIN}\n\nCheck console output at ${env.BUILD_URL} to view the results."
                )
            }
        }
    }
}